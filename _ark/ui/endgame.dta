#include endgame_handlers.dta
{new GHScreen post_show_screen
   (clear_vram TRUE)
   (panels GAME_PANELS fade_panel)
   (allow_back FALSE)
   (animate_transition FALSE)
   (enter
      {crowd_audio turn_off}
        {if {== career {gamecfg get mode}}
		{if {! $autoplay}
		{do
          ($score {player0 score})
		    ($new_cash)
            ($new_cash_reason)
            ($new_status)
            ($new_status_award)
            ($pfo {campaign is_unlocked multi_fo})
            ($already_won {campaign won_campaign})
            {campaign finish_song $score $new_cash $new_cash_reason $new_venue $new_status $new_status_award}
            {set [pfo_newly_unlocked] {&& {! $pfo} {campaign is_unlocked multi_fo}}}
            {cashaward_screen set new_cash $new_cash}
            {cashaward_screen set new_cash_reason $new_cash_reason}
            {if {! $already_won}
               {status_panel set new_status $new_status}}
            {status_panel set new_status_award $new_status_award}
            {if {!= $new_venue ""}
               {gamecfg set continue_screen career_screen}}}}}
		{do
         {script_task
            (units kTaskUISeconds)
            (delay 0.3)
            (script {play_sfx postshow_loop})}
         {ui goto_screen endgame_screen}}
    )}
{new EndGamePanel endgame_panel
   (file endgame.milo) 
   (focus me_continue.btn)
   (reset_focus TRUE)
   HEADLINE_HANDLERS
   ENDGAME_PANEL_HANDLERS
   (SELECT_START_MSG
      {switch $component
         (me_continue.btn {goto_endgame_complete})
         (me_morestats.btn
            {endgame_stats_screen set prev_screen {ui current_screen}}
            {ui goto_screen endgame_stats_screen})})}
{new GHScreen endgame_screen
   (panels GAME_PANELS endgame_panel helpbar)
   (focus endgame_panel)
   (clear_vram TRUE)
   (enter
      {world_panel set_showing FALSE})
   (in_game TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (helpbar
      (default
         ((fret1 help_continue)
            (strum help_updown))))}
{new GHPanel
   endgame_stats_panel
   (file sel_song.milo)
   (reset_focus TRUE)
   (focus 'ss_song.lst')
   ENDGAME_STATS_HANDLER}
{new GHScreen endgame_stats_screen
   (panels GAME_PANELS endgame_stats_panel helpbar)
   (focus endgame_stats_panel)
   (in_game TRUE)
   (allow_back TRUE)
   (animate_transition FALSE)
   (prev_screen 0)
   (next_screen 0)
   (helpbar
      (default
         ((fret1 help_continue)
            (fret2 help_back)
            (strum help_updown))))}
{new UIPanel highscore_panel
   (file
      {if_else $gamegh1
         highscore_gh1.milo
         highscore.milo})
   (active_ten 0)
   (auto_add TRUE)
   (flushed FALSE)
   HIGHSCORE_HANDLERS}
{new GHScreen highscore_screen
   (panels GAME_PANELS highscore_panel helpbar)
   (focus highscore_panel)
   (in_game TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (okay_to_add_score TRUE)
   (enter
      {set [okay_to_add_score] FALSE})
   (update_help_bar
      {do
         ($ten {highscore_panel get_active_ten})
         {helpbar set_display
            {if_else {&&
            {!= $ten NULL}
            {! {$ten is_done}}}
               {get_text_entry_help_text () $ten FALSE}
               {if_else {highscore_panel get flushed}
                  ()
                  ((fret1 help_continue))}}}})
   (SELECT_START_MSG
      {$this update_help_bar}
      kDataUnhandled)
   (SCREEN_BACK_MSG
      {synth play_sequence button_back.cue}
      {$this update_help_bar}
      kDataUnhandled)
   (TEXT_ENTRY_MSG
      {do
         ($ten {highscore_panel get_active_ten})
         {if {!= $ten NULL}
            {do
               ($text
                  {$ten get_text})
               {if [okay_to_add_score]
                  {set [okay_to_add_score] FALSE}
                  {highscores add $text {player0 score}}
                  {highscores set_default_name $text}}}}
         kDataUnhandled})
   (BUTTON_DOWN_MSG
      {if_else {== $button kPad_X}
         {do
            ($ten {highscore_panel get_active_ten})
            {if {'||'
                  {== $ten NULL}
                  {$ten is_done}}
               {synth play_sequence button_select}
               {ui goto_screen complete_screen}}
            kDataUnhandled}
         {if_else
            {highscore_panel get_active_ten}
            kDataUnhandled
            TRUE}})}
{new GHPanel complete_panel
   (file
      {if_else $gamegh1
         complete_gh1.milo
         complete.milo})
   (enter
      {$this set_focus comp_selsong.btn}
      {if_else {&&
      {== career {gamecfg get mode}}
      {! {campaign is_store_song {game get_song}}}}
         {comp_selsong.btn set_text CONTINUE}
         {comp_selsong.btn set_text comp_selsong}})
   (SELECT_START_MSG
      {switch $component
         (comp_selsong.btn {autosave_goto {gamecfg get continue_screen}})
         (comp_restart.btn {game_restart_fast})
         (comp_quit.btn {autosave_goto main_screen})}
      {stop_sfx postshow_loop}
      {if {== career {gamecfg get mode}}
         {gamecfg set continue_screen sel_song_screen}})}
{new GHScreen complete_screen
   (panels GAME_PANELS complete_panel helpbar)
   (focus complete_panel)
   (clear_vram TRUE)
   (in_game TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (helpbar
      (default
         ((fret1 help_continue)
            (strum help_updown))))}	